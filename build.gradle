plugins {
  id 'java-library'
  id 'net.ltgt.errorprone' version '3.1.0'
  id 'maven-publish'
}

group = 'io.github.takeyoda'

repositories {
  mavenCentral()
}

def encoding = 'UTF-8'

compileJava {
  options.encoding = encoding
  options.compilerArgs << '-parameters'
  options.compilerArgs << '-Xlint:deprecation'
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xdiags:verbose'
}

compileTestJava {
  options.encoding = encoding
  options.compilerArgs << '-Xlint:deprecation'
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xdiags:verbose'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
    vendor = JvmVendorSpec.AMAZON
    implementation = JvmImplementation.VENDOR_SPECIFIC
  }
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter'
  }
}

tasks.named('jar') {
  manifest {
    attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
  }
}

dependencies {
  implementation 'org.mybatis.generator:mybatis-generator-core:1.4.2'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
  testImplementation 'org.mockito:mockito-core:5.10.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'

  errorprone 'com.google.errorprone:error_prone_core:2.24.1'
}

publishing {
  repositories {
    maven {
      name = 'GitHubPackages'
      url = uri("https://maven.pkg.github.com/takeyoda/mybatis-generator-plugin-model-annotation")
      credentials {
        username = 'takeyoda'
        password = System.getenv('GITHUB_TOKEN')
      }
    }
  }
  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}
